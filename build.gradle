buildscript {
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
    }

    dependencies {
        classpath "org.jetbrains.intellij.plugins:gradle-intellij-plugin:0.2.0-SNAPSHOT"
    }
}

apply plugin: 'idea'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'

dependencies {
    compile fileTree('lib')
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

idea {
    module {
        name = 'IDLua'
    }
}

intellij {
    version = ideaVersion
    plugins = ['copyright']

    if (hasProperty("sources")) {
        downloadSources Boolean.valueOf(sources)
    }
    pluginName = 'Lua'
    type = "IC"
    updateSinceUntilBuild true
}

task getVersionTag(type: Exec) {
    commandLine 'hg', 'log', '-r', '\\".\\"', '--template', '{latesttag}'

    //store the output instead of printing to the console:
    standardOutput = new ByteArrayOutputStream()

    ext.output = {
        return standardOutput.toString()
    }
}

patchPluginXml {
    doFirst {
        version = getVersionTag.output
    }

    if (customSinceBuild) {
        sinceBuild = customSinceBuild
    }
}

patchPluginXml.dependsOn getVersionTag

group 'com.sylvanaar'

