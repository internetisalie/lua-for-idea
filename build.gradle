buildscript {
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
    }

    dependencies {
        classpath "org.jetbrains.intellij.plugins:gradle-intellij-plugin:0.2.0-SNAPSHOT"
    }
}

apply plugin: 'idea'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'

dependencies {
    compile fileTree('lib')
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

idea {
    module {
        name = 'IDLua'
    }
}

jar {
    archiveName = 'IDLua.jar'
}

intellij {
    version = ideaVersion
    plugins = ['copyright']
//    plugins = ['copyright', "PsiViewer:3.28.93"]

    if (hasProperty("sources")) {
        downloadSources Boolean.valueOf(sources)
    }
    pluginName = 'Lua'
    type = "IC"
    
    updateSinceUntilBuild false
    
    sinceBuild = sinceBuild
    untilBuild = untilBuild
}

import org.apache.tools.ant.taskdefs.condition.Os

task getVersionTag(type: Exec) {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'cmd.exe', '/c', 'hg', 'log', '-r', '\\".\\"', '--template', '{latesttag}'
    } else {
        commandLine 'sh', 'getlatesttag.sh'
    }

    //store the output instead of printing to the console:
    standardOutput = new ByteArrayOutputStream()

    ext.output = {
        return standardOutput.toString()
    }
}

patchPluginXml {
    dependsOn getVersionTag

    doFirst {
        version = getVersionTag.output
    }
}

task copyIncludes(type: Copy) {
    from 'include'
    into new File(prepareSandbox.destinationDir, intellij.pluginName)

    dependsOn prepareSandbox
}

prepareSandbox {
}

runIdea {
    dependsOn copyIncludes
}

buildPlugin {
    dependsOn copyIncludes
}

publishPlugin {
    doFirst {
        println("##teamcity[buildNumber '" + getVersionTag.output() + "']")
    }

    username publishUsername
    password publishPassword
}

group 'com.sylvanaar'

